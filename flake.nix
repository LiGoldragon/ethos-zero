{
  description = "protos — universal structural substrate";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    rust-build = {
      url = "github:LiGoldragon/rust-build";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    ethos-zero = {
      url = "github:LiGoldragon/ethos-zero/06d73e07ec7a133348e9e46511011a50f641b776";
    };
  };

  outputs = { self, nixpkgs, flake-utils, rust-build, ethos-zero }:
    flake-utils.lib.eachSystem [ "x86_64-linux" ] (system:
      let
        pkgs = import nixpkgs { inherit system; };
        rust = rust-build.lib.${system}.fromPkgs pkgs;
        inherit (rust) craneLib toolchain;
        src = rust.cleanSource { root = ./.; };
        common = { inherit src; strictDeps = true; cargoArtifacts = null; doInstallCargoArtifacts = false; };
      in {
        packages.default = craneLib.buildPackage common;
        checks = {
          build = craneLib.cargoBuild common;
          test = craneLib.cargoTest common;
          archival = craneLib.cargoTest (common // { cargoExtraArgs = "--locked --features rkyv"; });
          no-production-free-functions = pkgs.runCommand "protos-no-production-free-functions" { } ''
            if grep -R -n -E '^(pub(\([^)]*\))? )?fn ' ${src}/src; then
              echo "production Rust must not use module-level free functions" >&2
              exit 1
            fi
            touch $out
          '';
          no-production-inherent-methods = pkgs.runCommand "protos-no-production-inherent-methods" { } ''
            if grep -R -n -E '^[[:space:]]*impl[[:space:]]+[[:alpha:]_][[:alnum:]_:<>]*[[:space:]]*\{' ${src}/src; then
              echo "production Rust must home behavior in traits" >&2
              exit 1
            fi
            touch $out
          '';
          no-zst-behavior = pkgs.runCommand "protos-no-zst-behavior" { } ''
            if grep -R -n -E '^[[:space:]]*(pub[[:space:]]+)?struct[[:space:]]+[[:alpha:]_][[:alnum:]_]*[[:space:]]*;' ${src}/src; then
              echo "behavioral Rust nouns must carry data" >&2
              exit 1
            fi
            touch $out
          '';
          no-forbidden-vocabulary = pkgs.runCommand "protos-no-forbidden-vocabulary" { } ''
            if grep -R -n -i -E 'encode|decode|codec|transcode' ${src}/src; then
              echo "Protos names must use the ruled form vocabulary" >&2
              exit 1
            fi
            touch $out
          '';
          generated-kinds = pkgs.runCommand "protos-generated-kinds" {
            generator = ethos-zero.packages.${system}.default;
            declaration = ./protos-kinds.ethos;
            committed = ./generated/protos-kinds.rs;
          } (builtins.readFile ./checks/generated-kinds.sh);
          checked-anatomy = pkgs.runCommand "protos-checked-anatomy" {
            generator = ethos-zero.packages.${system}.default;
            declaration = ./protos.ethos;
          } (builtins.readFile ./checks/checked-anatomy.sh);
          doc = craneLib.cargoDoc (common // { RUSTDOCFLAGS = "-D warnings"; });
          fmt = craneLib.cargoFmt { inherit src; doInstallCargoArtifacts = false; };
          clippy = craneLib.cargoClippy (common // { cargoClippyExtraArgs = "--all-targets -- -D warnings"; });
        };
        devShells.default = pkgs.mkShell { packages = [ pkgs.jujutsu toolchain ]; };
      });
}
